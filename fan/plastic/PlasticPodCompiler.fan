using concurrent::AtomicInt
using compiler

@NoDoc
const class PlasticPodCompiler {

	** static because pods are shared throughout the JVM, not just the IoC 
	private static const AtomicInt podIndex	:= AtomicInt(1)

	// based on http://fantom.org/sidewalk/topic/2127#c13844
	Pod compile(Str fantomPodCode, Str? podName := null) {

		podName = podName ?: generatePodName
		
		try {
			input 		    := CompilerInput()
			input.podName 	= podName
	 		input.summary 	= "Alien-Factory Transient Pod"
			input.version 	= Version.defVal
			input.log.level = LogLevel.warn
			input.isScript 	= true
			input.output 	= CompilerOutputMode.transientPod
			input.mode 		= CompilerInputMode.str
			input.srcStrLoc	= Loc(podName)
			input.srcStr 	= fantomPodCode
	
			compiler 		:= Compiler(input)
			pod 			:= compiler.compile.transientPod
			return pod

		} catch (CompilerErr err) {
			srcErrLoc := SrcErrLocation(`${podName}`, fantomPodCode, err.line, err.msg)
			throw PlasticCompilationErr(srcErrLoc, 5)
		}
	}
	
	** Different pod names prevents "sys::Err: Duplicate pod name: <podName>".
	** We internalise podName so we can guarantee no dup pod names
	Str generatePodName() {
		"${PlasticPodCompiler#.pod.name}Pod" + "$podIndex.getAndIncrement".padl(3, '0')
	}
}

